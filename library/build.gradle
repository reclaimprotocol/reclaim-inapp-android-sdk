plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id 'maven-publish'
}

android {
    namespace 'org.reclaimprotocol.inapp_sdk'
    compileSdk 36

    defaultConfig {
        minSdk 24
        multiDexEnabled true

        ndk {
            // Filter for architectures supported by Flutter
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        profile {
            initWith debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    String flutterStorageUrl = System.env.FLUTTER_STORAGE_BASE_URL ?: "https://storage.googleapis.com"
    String reclaimStorageUrl = System.env.RECLAIM_STORAGE_BASE_URL ?: "https://reclaim-inapp-sdk.s3.ap-south-1.amazonaws.com/android/repo"
    repositories {
        google()
        mavenCentral()
        maven {
            url "$reclaimStorageUrl"
        }
        maven {
            url "$flutterStorageUrl/download.flutter.io"
        }
    }
    publishing {
        singleVariant("release") {}
        // multipleVariants {
        //     allVariants()
        //     withJavadocJar()
        // }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    debugImplementation libs.reclaim.debug
    profileImplementation libs.reclaim.profile
    releaseImplementation libs.reclaim.release
}
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'org.reclaimprotocol'
            version = "$System.env.INAPP_SDK_VERSION"
            artifactId = 'inapp_sdk'

            afterEvaluate {
                from components.release
            }
        }
    }
}